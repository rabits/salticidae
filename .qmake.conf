# Project Version
VERSION='1.0'

VERSION_STR='\\"$${VERSION}\\"'
DEFINES += PROJECT_VERSION=\"$${VERSION_STR}\"

# Get available plugins & add it to build and deploy
plugins_types_list = $$files("plugins/*")
plugins_types = $$split(plugins_types_list, " ")
for(plugin_type_dir, plugins_types) {
    plugin_type = $$basename(plugin_type_dir)
    plugin_dirs_list = $$files("$${plugin_type_dir}/*")
    plugin_dirs = $$split(plugin_dirs_list, " ")
    for(plugin_dir, plugin_dirs) {
        plugin_name = $$basename(plugin_dir)
        exists("$${plugin_dir}/$${plugin_name}.pro") {
            message("Found and added plugin-$${plugin_type}-$${plugin_name}")
            SUBDIRS += "$${plugin_dir}"
            ANDROID_EXTRA_LIBS += "$${plugin_dir}/libplugin-$${plugin_type}-$${plugin_name}.so"
        }
    }
}
